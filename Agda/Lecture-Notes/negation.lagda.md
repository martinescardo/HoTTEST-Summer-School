
[Martin Escardo](Https://www.Cs.Bham.Ac.Uk/~mhe/).
Notes originally written for the module *Advanced Functional Programming* of the [University of Birmingham](https://www.birmingham.ac.uk/index.aspx), UK.


<!--
```agda
{-# OPTIONS --without-K --safe #-}

module negation where

open import general-notation
open import prelude
```
-->
# Reasoning with negation

[Recall that](empty-type.lagda.md) we defined the negation `Â¬ A` of a type `A` to be the function type `A â†’ ğŸ˜`,
and that we also wrote `is-empty A` as a synonym of `Â¬ A`.

## Emptiness of the empty type

We have the following two proofs of "not false" or "the empty type is empty":
```agda
not-false : Â¬ ğŸ˜
not-false = ğŸ˜-elim

not-false' : Â¬ ğŸ˜
not-false' = id
```
A lot of things about negation don't depend on the fact that the target type of the function type is `ğŸ˜`. We will begin by proving some things about negation by generalizing `ğŸ˜` to any type `R` of "results".

## Implication from disjunction and negation

If `Â¬ A` or `B`, then `A implies B`:
```agda
implication-from-disjunction-and-negation : {A B : Type} â†’ Â¬ A âˆ” B â†’ (A â†’ B)
implication-from-disjunction-and-negation (inl f) a = ğŸ˜-elim (f a)
implication-from-disjunction-and-negation (inr b) a = b
```


## Contrapositives

If `A` implies `B`, then `B â†’ R` implies `A â†’ R`:
```agda
arrow-contravariance : {A B R : Type}
                     â†’ (A â†’ B)
                     â†’ (B â†’ R) â†’ (A â†’ R)
arrow-contravariance f g = g âˆ˜ f
```
A particular case of interest is the following. The [contrapositive](https://en.wikipedia.org/wiki/Contraposition) of an implication `A â†’ B` is the implication `Â¬ B â†’ Â¬ A`:
```agda
contrapositive : {A B : Type} â†’ (A â†’ B) â†’ (Â¬ B â†’ Â¬ A)
contrapositive {A} {B} = arrow-contravariance {A} {B} {ğŸ˜}
```
This can also be read as "if we have a function A â†’ B and B is empty, then also A must be empty".

## Double and triple negations

We now introduce notation for double and triple negation, to reduce the number of needed brackets:

```agda
Â¬Â¬_ Â¬Â¬Â¬_ : Type â†’ Type
Â¬Â¬  A = Â¬(Â¬ A)
Â¬Â¬Â¬ A = Â¬(Â¬Â¬ A)
```
We have that `A` implies `Â¬Â¬ A`. This is called double negation introduction. More generally, we have the following:
```agda
dni : (A R : Type) â†’ A â†’ ((A â†’ R) â†’ R)
dni A R a u = u a

Â¬Â¬-intro : {A : Type} â†’ A â†’ Â¬Â¬ A
Â¬Â¬-intro {A} = dni A ğŸ˜
```
We don't always have `Â¬Â¬ A â†’ A` in proofs-as-programs. This has to do with *computability theory*. But sometimes we do. For example, if we know that `A âˆ” Â¬ A` then `Â¬Â¬A â†’ A` follows:
<!--
```agda
private -- because it is defined elsewhere, and it is here for illustration only
```
-->
```agda
 Â¬Â¬-elim : {A : Type} â†’ A âˆ” Â¬ A â†’ Â¬Â¬ A â†’ A
 Â¬Â¬-elim (inl x) f = x
 Â¬Â¬-elim (inr g) f = ğŸ˜-elim (f g)
```
For more details, see the lecture notes on [decidability](decidability.lagda.md), where we discuss `Â¬Â¬-elim` again.
But three negations always imply one, and conversely:
```agda
three-negations-imply-one : {A : Type} â†’ Â¬Â¬Â¬ A â†’ Â¬ A
three-negations-imply-one = contrapositive Â¬Â¬-intro

one-negation-implies-three : {A : Type} â†’ Â¬ A â†’ Â¬Â¬Â¬ A
one-negation-implies-three = Â¬Â¬-intro
```

## Negation of the identity type

It is useful to introduce a notation for the negation of the [identity type](identity-type.lagda.md):
```agda
_â‰¢_ : {X : Type} â†’ X â†’ X â†’ Type
x â‰¢ y = Â¬ (x â‰¡ y)

â‰¢-sym : {X : Type} {x y : X} â†’ x â‰¢ y â†’ y â‰¢ x
â‰¢-sym = contrapositive sym

false-is-not-true : false â‰¢ true
false-is-not-true ()

true-is-not-false : true â‰¢ false
true-is-not-false ()
```
The following is more interesting:
```agda
not-false-is-true : (x : Bool) â†’ x â‰¢ false â†’ x â‰¡ true
not-false-is-true true  f = refl true
not-false-is-true false f = ğŸ˜-elim (f (refl false))

not-true-is-false : (x : Bool) â†’ x â‰¢ true â†’ x â‰¡ false
not-true-is-false true  f = ğŸ˜-elim (f (refl true))
not-true-is-false false f = refl false
```

## Disjointness of binary sums

We now show something that is intuitively the case:
```agda
inl-is-not-inr : {X Y : Type} {x : X} {y : Y} â†’ inl x â‰¢ inr y
inl-is-not-inr ()
```
Agda just knows it.

## Disjunctions and negation

If  `A or B` holds and `B` is false, then `A` must hold:
```agda
right-fails-gives-left-holds : {A B : Type} â†’ A âˆ” B â†’ Â¬ B â†’ A
right-fails-gives-left-holds (inl a) f = a
right-fails-gives-left-holds (inr b) f = ğŸ˜-elim (f b)

left-fails-gives-right-holds : {A B : Type} â†’ A âˆ” B â†’ Â¬ A â†’ B
left-fails-gives-right-holds (inl a) f = ğŸ˜-elim (f a)
left-fails-gives-right-holds (inr b) f = b
```

## Negation of the existential quantifier:

If there is no `x : X` with `A x`, then for all `x : X` not `A x`:
```agda
not-exists-implies-forall-not : {X : Type} {A : X â†’ Type}
                              â†’ Â¬ (Î£ x ê‰ X , A x)
                              â†’ (x : X) â†’ Â¬ A x
not-exists-implies-forall-not f x a = f (x , a)
```
The converse also holds:
```agda
forall-not-implies-not-exists : {X : Type} {A : X â†’ Type}
                              â†’ ((x : X) â†’ Â¬ A x)
                              â†’ Â¬ (Î£ x ê‰ X , A x)
forall-not-implies-not-exists g (x , a) = g x a
```
Notice how these are particular cases of [`curry` and `uncurry`](https://en.wikipedia.org/wiki/Currying).

## Implication truth table

Here is a proof of the implication truth-table:
```agda
open import empty-type
open import unit-type

implication-truth-table : ((ğŸ˜ â†’ ğŸ˜) â‡” ğŸ™)
                        Ã— ((ğŸ˜ â†’ ğŸ™) â‡” ğŸ™)
                        Ã— ((ğŸ™ â†’ ğŸ˜) â‡” ğŸ˜)
                        Ã— ((ğŸ™ â†’ ğŸ™) â‡” ğŸ™)
implication-truth-table = ((Î» _ â†’ â‹†)   , (Î» _ â†’ id)) ,
                          ((Î» _ â†’ â‹†)   , (Î» _ _ â†’ â‹†)) ,
                          ((Î» f â†’ f â‹†) , (Î» â‹† _ â†’ â‹†)) ,
                          ((Î» _ â†’ â‹†)   , (Î» _ _ â†’ â‹†))
```

[Go back to the table of contents](https://martinescardo.github.io/HoTTEST-Summer-School/)
