
[Martin Escardo](Https://www.Cs.Bham.Ac.Uk/~mhe/).
Notes originally written for the module *Advanced Functional Programming* of the [University of Birmingham](https://www.birmingham.ac.uk/index.aspx), UK.


<!--
```agda
{-# OPTIONS --without-K --safe #-}

module Vector-functions where

open import prelude
```
-->
# Some functions on vectors

As mentioned in the [introduction](introduction.lagda.md), vectors allow us to have safe `head` and `tail` functions.
```agda
head : {A : Type} {n : ‚Ñï} ‚Üí Vector A (suc n) ‚Üí A
head (x :: xs) = x

tail : {A : Type} {n : ‚Ñï} ‚Üí Vector A (suc n) ‚Üí Vector A n
tail (x :: xs) = xs
```

We can also define a safe indexing function on vectors using [finite types](Fin.lagda.md) as follows.
```agda
open import Fin

_!!_ : ‚àÄ {X n} ‚Üí Vector X n ‚Üí Fin n ‚Üí X
(x :: xs) !! zero  = x
(x :: xs) !! suc n = xs !! n
```
Alternatively, this can be defined as follows:
```agda
_!!'_ : {X : Type} {n : ‚Ñï} ‚Üí Vector X n ‚Üí Fin n ‚Üí X
[]        !!' ()
(x :: xs) !!' zero  = x
(x :: xs) !!' suc n = xs !!' n
```

We can also do vector concatenation:
```agda
_++_ : {X : Type} {m n : ‚Ñï} ‚Üí Vector X m ‚Üí Vector X n ‚Üí Vector X (m + n)
[]        ++ ys = ys
(x :: xs) ++ ys = x :: (xs ++ ys)

infixr 20 _++_
```

## Vectors represented as a Basic MLTT type

```agda
open import unit-type
open import binary-products

Vector' : Type ‚Üí ‚Ñï ‚Üí Type
Vector' A 0       = ùüô
Vector' A (suc n) = A √ó Vector' A n

[]' : {A : Type} ‚Üí Vector' A 0
[]' = ‚ãÜ

_::'_ : {A : Type} {n : ‚Ñï} ‚Üí A ‚Üí Vector' A n ‚Üí Vector' A (suc n)
x ::' xs = x , xs

infixr 10 _::'_

private
 example‚ÇÄ : Vector' ‚Ñï 3
 example‚ÇÄ = 1 ::' 2 ::' 3 ::' ([]' {‚Ñï})

 example‚ÇÅ : example‚ÇÄ ‚â° (1 , 2 , 3 , ‚ãÜ)
 example‚ÇÅ = refl _

open import isomorphisms

Vector-iso : {A : Type} {n : ‚Ñï} ‚Üí Vector A n ‚âÖ Vector' A n
Vector-iso {A} {n} = record { bijection = f n ; bijectivity = f-is-bijection n }
 where
  f : (n : ‚Ñï) ‚Üí Vector A n ‚Üí Vector' A n
  f 0       []        = []' {A}
  f (suc n) (x :: xs) = x ::' f n xs

  g : (n : ‚Ñï) ‚Üí Vector' A n ‚Üí Vector A n
  g 0       ‚ãÜ        = []
  g (suc n) (x , xs) = x :: g n xs

  gf : (n : ‚Ñï) ‚Üí g n ‚àò f n ‚àº id
  gf 0       []        = refl []
  gf (suc n) (x :: xs) = ap (x ::_) (gf n xs)

  fg : (n : ‚Ñï) ‚Üí f n ‚àò g n ‚àº id
  fg 0       ‚ãÜ        = refl ‚ãÜ
  fg (suc n) (x , xs) = ap (x ,_) (fg n xs)

  f-is-bijection : (n : ‚Ñï) ‚Üí is-bijection (f n)
  f-is-bijection n = record { inverse = g n ; Œ∑ = gf n ; Œµ = fg n }

private
 open _‚âÖ_
 open is-bijection

 example‚ÇÇ : bijection Vector-iso (1 :: 2 :: 3 :: []) ‚â° (1 , 2 , 3 , ‚ãÜ)
 example‚ÇÇ = refl _

 example‚ÇÑ : Vector ‚Ñï 3
 example‚ÇÑ = inverse (bijectivity Vector-iso) (1 , 2 , 3 , ‚ãÜ)

 example‚ÇÖ : example‚ÇÑ ‚â° 1 :: 2 :: 3 :: []
 example‚ÇÖ = refl _
```

[Go back to the table of contents](https://martinescardo.github.io/HoTTEST-Summer-School/)
