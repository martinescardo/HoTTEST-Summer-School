
[Martin Escardo](Https://www.Cs.Bham.Ac.Uk/~mhe/).
Notes originally written for the module *Advanced Functional Programming* of the [University of Birmingham](https://www.birmingham.ac.uk/index.aspx), UK.

<!--
```agda
{-# OPTIONS --cubical-compatible --safe #-}

module introduction where
```

**Warning for maintainers of the lecture notes**. This module should not be imported from any module other than natural-numbers-type.lagda.md. The reason the import is needed there is that the pragma {-# BUILTIN NATURAL â„• #-} cannot be used in two modules, but the build of these lecture notes requires importing all files.

-->
# Introduction to Agda

Everything defined and briefly discussed in this introduction will be redefined and discussed in more detail in other handouts.

## Initial examples of types in Agda

<!--
In Agda, types are called sets by default. For the purposes of HoTT/UF, we prefer to stick to "Type".
```agda
Type = Set
```
-->

Here are some examples of types:


```agda
data Bool : Type where
 true false : Bool

data â„• : Type where
 zero : â„•
 suc  : â„• â†’ â„•

{-# BUILTIN NATURAL â„• #-}

data List (A : Type) : Type where
 []   : List A
 _::_ : A â†’ List A â†’ List A

infixr 10 _::_
```

The pragma `BUILTIN NATURAL` is to get syntax sugar to be able to write 0,1,2,3,... rather than the more verbose

 * zero
 * suc zero
 * suc (suc zero)
 * suc (suc (suc zero))
 * â‹¯

We pronounce `suc` as "successor".

## Examples of definitions using the above types

```agda
not : Bool â†’ Bool
not true  = false
not false = true

_&&_ : Bool â†’ Bool â†’ Bool
true  && y = y
false && y = false

_||_ : Bool â†’ Bool â†’ Bool
true  || y = true
false || y = y

infixr 20 _||_
infixr 30 _&&_

if_then_else_ : {A : Type} â†’ Bool â†’ A â†’ A â†’ A
if true  then x else y = x
if false then x else y = y

_+_ : â„• â†’ â„• â†’ â„•
zero  + y = y
suc x + y = suc (x + y)

_*_ : â„• â†’ â„• â†’ â„•
zero  * y = 0
suc x * y = x * y + y

infixr 20 _+_
infixr 30 _*_

sample-listâ‚€ : List â„•
sample-listâ‚€ = 1 :: 2 :: 3 :: []

sample-listâ‚ : List Bool
sample-listâ‚ = true || false && true :: false :: true :: true :: []

length : {A : Type} â†’ List A â†’ â„•
length []        = 0
length (x :: xs) = 1 + length xs

_++_ : {A : Type} â†’ List A â†’ List A â†’ List A
[]        ++ ys = ys
(x :: xs) ++ ys = x :: (xs ++ ys)

infixr 20 _++_
```

## More sophisticated examples of types in Agda

Sometimes we may wish to consider lists over a type `A` of a given length `n : â„•`. The elements of this type, written `Vector A n`, are called *vectors*, and the type can be defined as follows:

```agda
data Vector (A : Type) : â„• â†’ Type where
 []   : Vector A 0
 _::_ : {n : â„•} â†’ A â†’ Vector A n â†’ Vector A (suc n)
```
This is called a *dependent type* because it is a type that depends on *elements* `n` of another type, namely `â„•`.

In Agda, we can't define the `head` and `tail` functions on lists, because they are undefined for the empty list, and functions must be total in Agda. Vectors solve this problem:

```agda
head : {A : Type} {n : â„•} â†’ Vector A (suc n) â†’ A
head (x :: xs) = x

tail : {A : Type} {n : â„•} â†’ Vector A (suc n) â†’ Vector A n
tail (x :: xs) = xs
```
Agda accepts the above definitions because it knows that the input vector has at least one element, and hence does have a head and a tail. Here is another example.

Dependent types are pervasive in Agda.

## The empty type and the unit type

A type with no elements can be defined as follows:
```agda
data ğŸ˜ : Type where
```
We will also need the type with precisely one element, which we define as follows:
```agda
data ğŸ™ : Type where
 â‹† : ğŸ™
```

Here is an example of a dependent type defined using the above types:
```agda
_â‰£_ : â„• â†’ â„• â†’ Type
0     â‰£ 0     = ğŸ™
0     â‰£ suc y = ğŸ˜
suc x â‰£ 0     = ğŸ˜
suc x â‰£ suc y = x â‰£ y

infix 0 _â‰£_
```
The idea of the above definition is that `x â‰£ y` is a type which either has precisely one element, if `x` and `y` are the same natural number, or else is empty, if `x` and `y` are different.
The following definition says that for any natural number `x` we can find an element of the type `x â‰£ x`.
```agda
â„•-refl : (x : â„•) â†’ x â‰£ x
â„•-refl 0       = â‹†
â„•-refl (suc x) = â„•-refl x
```
## The identity type former `_â‰¡_`

It is possible to generalize the above definition
for any type in place of that of natural numbers as follows:
```agda
data _â‰¡_ {A : Type} : A â†’ A â†’ Type where
 refl : (x : A) â†’ x â‰¡ x

infix 0 _â‰¡_
```
Here are some functions we can define with the identity type:
```agda
trans : {A : Type} {x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
trans (refl x) (refl x) = refl x

sym : {A : Type} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
sym (refl x) = refl x

ap : {A B : Type} (f : A â†’ B) {x y : A} â†’ x â‰¡ y â†’ f x â‰¡ f y
ap f (refl x) = refl (f x)
```

The identity type is a little bit subtle and there is a lot to say about it.
For the moment, let's convince ourselves that we can convert back and forth between the types `x â‰¡ y` and `x â‰£ y`, in the case that `A` is the type of natural numbers, as follows:

```agda
back : (x y : â„•) â†’ x â‰¡ y â†’ x â‰£ y
back x x (refl x) = â„•-refl x

forth : (x y : â„•) â†’ x â‰£ y â†’ x â‰¡ y
forth 0       0       â‹† = refl 0
forth (suc x) (suc y) p = I
 where
  IH : x â‰¡ y
  IH = forth x y p

  I : suc x â‰¡ suc y
  I = ap suc IH
```

## Propositions as types

The [CurryHoward interpretation of logic](https://en.wikipedia.org/wiki/Curry%E2%80%93Howard_correspondence), after [Haskell Curry](https://en.wikipedia.org/wiki/Haskell_Curry) and [William Howard](https://en.wikipedia.org/wiki/William_Alvin_Howard), interprets logical statements, also known as propositions, as *types*. [Per Martin-LÃ¶f](https://en.wikipedia.org/wiki/Per_Martin-L%C3%B6f) extended this interpretation of propositions as types with equality, by introducing the identity type discussed above.

An incomplete table of the CurryHoward--Martin-LÃ¶f interpretation of logical propositions is the following:

| Proposition  | Type                                  |
| -          | ---                                   |
| A implies B  | function type A â†’ B                   |
| âˆ€ x : A, B x | dependent function type (x : A) â†’ B x |
| equality     | identity type `_â‰¡_`                   |

This was enough for our examples above.

For more complex examples of reasoning about programs, we need to complete the following table:

| Logic        | English                    | Type          |
| -          | ---                        | ---           |
| Â¬ A          | not A                      | ?             |
| A âˆ§ B        | A and B                    | ?             |
| A âˆ¨ B        | A or B                     | ?             |
| A â†’ B        | A implies B                | A â†’ B         |
| âˆ€ x : A, B x | for all x:A, B x           | (x : A) â†’ B x |
| âˆƒ x : A, B x | there is x:A such that B x | ?             |
| x = y        | x equals y                 | x â‰¡ y         |

This will be the subject of future handouts.

## Agda manual

Please study the [Agda manual](https://agda.readthedocs.io/en/latest/) as a complement to these lecture notes.

[Go back to the table of contents](https://martinescardo.github.io/HoTTEST-Summer-School/)
