# Week 9 - Cubical Agda Exercises

## Standard disclaimer:

**The exercises are designed to increase in difficulty so that we can cater to
our large and diverse audience. This also means that it is *perfectly fine* if
you don't manage to do all exercises: some of them are definitely a bit hard for
beginners and there are likely too many exercises! You *may* wish to come back
to them later when you have learned more.**

Having said that, here we go!

In case you haven't done the other Agda exercises:
This is a markdown file with Agda code, which means that it displays nicely on
GitHub, but at the same time you can load this file in Agda and fill the holes
to solve exercises.

**When solving the problems,
please make a copy of this file to work in, so that it doesn't get overwritten
(in case we update the exercises through `git`)!**


```agda
{-# OPTIONS --cubical --allow-unsolved-metas #-}

module Solutions9 where

open import cubical-prelude
open import Lecture7-notes
open import Lecture8-notes
open import Lecture9-notes
open import Solutions7 hiding (rUnit)
open import Solutions8
open import Lecture9-live using (SemiGroupâ„•)
```

## Part I: More hcomps

### Exercise 1 (â˜…â˜…)
### (a)
Show the left cancellation law for path composition using an hcomp.
Hint: one hcomp should suffice. Use `comp-filler` and connections

```agda
lUnit : {A : Type â„“} {x y : A} (p : x â‰¡ y) â†’ refl âˆ™ p â‰¡ p
lUnit {x = x} {y = y} p i j =
  hcomp (Î» k â†’ Î» {(i = i0) â†’ comp-filler refl p k j
                 ; (i = i1) â†’ p (k âˆ§ j)
                 ; (j = i0) â†’ x
                 ; (j = i1) â†’ p k})
        x


```
### (b)
Try to mimic the construction of lUnit for rUnit (i.e. redefine it)
in such a way that `rUnit refl â‰¡ lUnit refl` holds by `refl`.
Hint: use (almost) the exact same hcomp.

```agda
rUnit : {A : Type â„“} {x y : A} (p : x â‰¡ y) â†’ p âˆ™ refl â‰¡ p
rUnit {x = x} {y = y} p i j =
  hcomp (Î» k â†’ Î» {(i = i0) â†’ comp-filler p refl k j
                 ; (i = i1) â†’ p j
                 ; (j = i0) â†’ x
                 ; (j = i1) â†’ y})
        (p j)

-- uncomment to see if it type-checks

{-
rUnitâ‰¡lUnit : âˆ€ {â„“} {A : Type â„“} {x : A} â†’ rUnit (refl {x = x}) â‰¡ lUnit refl
rUnitâ‰¡lUnit = refl
-}

```


### Exercise 2 (â˜…â˜…)
Show the associativity law for path composition
Hint: one hcomp should suffice. This one can be done without connections
  (but you might need comp-filler in more than one place)

```agda
assoc : {A : Type â„“} {x y z w : A} (p : x â‰¡ y) (q : y â‰¡ z) (r : z â‰¡ w)
  â†’ p âˆ™ (q âˆ™ r) â‰¡ (p âˆ™ q) âˆ™ r
assoc {x = x} {y = y} p q r i j =
  hcomp (Î» k â†’ Î» {(i = i0) â†’ (p âˆ™ comp-filler q r k) j
                 ; (i = i1) â†’ comp-filler (p âˆ™ q) r k j
                 ; (j = i0) â†’ x
                 ; (j = i1) â†’ r k})
        ((p âˆ™ q) j)

```

### Exercise 3 (Master class in connections) (ğŸŒ¶)
The goal of this exercise is to give a cubical proof of the Eckmann-Hilton argument,
which says that path composition for higher loops is commutative

(a) While we cannot get `p âˆ™ q â‰¡ q âˆ™ p` as a one-liner, we can get a
one-liner showing that the identiy holds up to some annoying
coherences.  Try to understand the following statement (and why it's
well-typed). After that, fill the holes

Hint: each hole will need a `âˆ¨` or a `âˆ§`

```agda
pre-EH : {A : Type â„“} {x : A} (p q : refl {x = x} â‰¡ refl)
  â†’ ap (Î» x â†’ x âˆ™ refl) p âˆ™ ap (Î» x â†’ refl âˆ™ x) q
   â‰¡ ap (Î» x â†’ refl âˆ™ x) q âˆ™ ap (Î» x â†’ x âˆ™ refl) p
pre-EH {x = x} p q i = (Î» j â†’ p (~ i âˆ§ j) âˆ™ q (i âˆ§ j))
                     âˆ™ (Î» j â†’ p (~ i âˆ¨ j) âˆ™ q (i âˆ¨ j))

```
(b) If we manage to cancel out all of the annoying aps, we get Eckmann-Hilton:
For paths (p q : refl â‰¡ refl), we have p âˆ™ q â‰¡ q âˆ™ p. Try to prove this, using the above lemma.

Hint: Use the pre-EH as the bottom of an hcomp (one should be enough).
For the sides, use lUnit and rUnit wherever they're needed. Note that this will only work out smoothly if
you've solved Exercise 1 (b).

```agda
Eckmann-Hilton : {A : Type â„“} {x : A} (p q : refl {x = x} â‰¡ refl) â†’ p âˆ™ q â‰¡ q âˆ™ p
Eckmann-Hilton {x = x} p q k i =
  hcomp (Î» r â†’ Î» {(i = i0) â†’ rUnit (refl {x = x}) r
                 ; (i = i1) â†’ rUnit (refl {x = x}) r
                 ; (k = i0) â†’ ((Î» j â†’ rUnit (p j) r) âˆ™ (Î» j â†’ lUnit (q j) r)) i
                 ; (k = i1) â†’ ((Î» j â†’ lUnit (q j) r) âˆ™ (Î» j â†’ rUnit (p j) r)) i})
        (pre-EH p q k i)

```
# # Part 2: Binary numbers as a HIT
Here is another HIT describing binary numbers. The idea is that a binary number is a list of booleans, modulo trailing zeros.

For instance, `true âˆ· true âˆ· true âˆ· []` is the binary number 110 ...
... and so is `true âˆ· true âˆ· false âˆ· false âˆ· false âˆ· []`

(!) Note that we're interpreting 110 as 1Â·2â° + 1Â·2Â¹ + 0Â·2Â² here.

```agda
0B = false
1B = true

data ListBin : Type where
  []    : ListBin
  _âˆ·_   : (x : Bool) (xs : ListBin) â†’ ListBin
  drop0 : 0B âˆ· [] â‰¡ []

-- 1 as a binary number
1LB : ListBin
1LB = 1B âˆ· []
```
### Exercise 4 (â˜…)
Define the successor function on ListBin
```agda



sucListBin : ListBin â†’ ListBin
sucListBin [] = 1LB
sucListBin (true âˆ· xs) = false âˆ· sucListBin xs
sucListBin (false âˆ· xs) = 1B âˆ· xs
sucListBin (drop0 i) = 1LB

```
### Exercise 5 (â˜…â˜…)
Define an addition `+LB` on ListBin and prove that `x +LB [] â‰¡ x`
Do this by mutual induction! Make sure the three cases for the right unit law hold by refl.
```agda

_+LB_ : ListBin â†’ ListBin â†’ ListBin
rUnit+LB : (x : ListBin) â†’ x +LB [] â‰¡ x
[] +LB y = y
(x âˆ· xs) +LB [] = x âˆ· xs
(true âˆ· xs) +LB (true âˆ· ys) = false âˆ· sucListBin (xs +LB ys)
(true âˆ· xs) +LB (false âˆ· ys) = true âˆ· (xs +LB ys)
(false âˆ· xs) +LB (y âˆ· ys) = y âˆ· (xs +LB ys)
(true âˆ· xs) +LB drop0 i = true âˆ· (rUnit+LB xs i)
(false âˆ· xs) +LB drop0 i = false âˆ· (rUnit+LB xs i)
drop0 i +LB [] = drop0 i
drop0 i +LB (y âˆ· ys) = y âˆ· ys
drop0 i +LB drop0 j = drop0 (j âˆ§ i)
rUnit+LB [] = refl
rUnit+LB (x âˆ· xâ‚) = refl
rUnit+LB (drop0 i) = refl

```
(c) Prove that sucListBin is left distributive over `+LB`
Hint: If you pattern match deep enough, there should be a lot of refls...
```agda

sucListBinDistrL : (x y : ListBin) â†’ sucListBin (x +LB y) â‰¡ (sucListBin x +LB y)
sucListBinDistrL [] [] = refl
sucListBinDistrL [] (true âˆ· y) = refl
sucListBinDistrL [] (false âˆ· y) = refl
sucListBinDistrL [] (drop0 i) = refl
sucListBinDistrL (true âˆ· xs) [] = refl
sucListBinDistrL (false âˆ· xs) [] = refl
sucListBinDistrL (true âˆ· xs) (true âˆ· y) = ap (1B âˆ·_) (sucListBinDistrL xs y)
sucListBinDistrL (true âˆ· xs) (false âˆ· y) = ap (0B âˆ·_) (sucListBinDistrL xs y)
sucListBinDistrL (false âˆ· xs) (true âˆ· y) = refl
sucListBinDistrL (false âˆ· xs) (false âˆ· y) = refl
sucListBinDistrL (true âˆ· []) (drop0 i)  = refl
sucListBinDistrL (true âˆ· (true âˆ· xs)) (drop0 i) = refl
sucListBinDistrL (true âˆ· (false âˆ· xs)) (drop0 i) = refl
sucListBinDistrL (true âˆ· drop0 i) (drop0 j) = refl
sucListBinDistrL (false âˆ· xs) (drop0 i) = refl
sucListBinDistrL (drop0 i) [] = refl
sucListBinDistrL (drop0 i) (true âˆ· y) = refl
sucListBinDistrL (drop0 i) (false âˆ· y) = refl
sucListBinDistrL (drop0 i) (drop0 j) = refl
```

### Exercise 6 (â˜…)
Define a map `LBâ†’â„• : ListBin â†’ â„•` and show that it preserves addition

```agda
â„•â†’ListBin : â„• â†’ ListBin
â„•â†’ListBin zero = []
â„•â†’ListBin (suc x) = sucListBin (â„•â†’ListBin x)

â„•â†’ListBin-pres+ : (x y : â„•) â†’ â„•â†’ListBin (x + y) â‰¡ (â„•â†’ListBin x +LB â„•â†’ListBin y)
â„•â†’ListBin-pres+ zero y = refl
â„•â†’ListBin-pres+ (suc x) zero =
  ap sucListBin (ap â„•â†’ListBin (+-comm x zero))
  âˆ™ sym (rUnit+LB (sucListBin (â„•â†’ListBin x)))
â„•â†’ListBin-pres+ (suc x) (suc y) =
  ap sucListBin (â„•â†’ListBin-pres+ x (suc y))
   âˆ™ sucListBinDistrL (â„•â†’ListBin x) (sucListBin (â„•â†’ListBin y))

```

### Exercise 7 (â˜…â˜…â˜…)
Show that `â„• â‰ƒ ListBin`.

```agda

-- useful lemma
move4 : (x y z w : â„•) â†’ (x + y) + (z + w) â‰¡ (x + z) + (y + w)
move4 x y z w =
  (x + y) + (z + w)   â‰¡âŸ¨ +-assoc (x + y) z w âŸ©
  ((x + y) + z) + w   â‰¡âŸ¨ ap (_+ w) (sym (+-assoc x y z)
                      âˆ™ ap (x +_) (+-comm y z)
                      âˆ™ (+-assoc x z y)) âŸ©
  ((x + z) + y) + w   â‰¡âŸ¨ sym (+-assoc (x + z) y w) âŸ©
  ((x + z) + (y + w)) âˆ

ListBinâ†’â„• : ListBin â†’ â„•
ListBinâ†’â„• [] = 0
ListBinâ†’â„• (true âˆ· xs) = suc (2 Â· ListBinâ†’â„• xs)
ListBinâ†’â„• (false âˆ· xs) = 2 Â· ListBinâ†’â„• xs
ListBinâ†’â„• (drop0 i) = 0

ListBinâ†’â„•-suc : (x : ListBin) â†’ ListBinâ†’â„• (sucListBin x) â‰¡ suc (ListBinâ†’â„• x)
ListBinâ†’â„•-suc [] = refl
ListBinâ†’â„•-suc (true âˆ· xs) =
   ap (Î» x â†’ x + x) (ListBinâ†’â„•-suc xs)
 âˆ™ ap suc (sym (+-suc (ListBinâ†’â„• xs) (ListBinâ†’â„• xs)))
ListBinâ†’â„•-suc (false âˆ· xs) = refl
ListBinâ†’â„•-suc (drop0 i) = refl

x+x-charac : (xs : ListBin) â†’ (xs +LB xs) â‰¡ (false âˆ· xs)
x+x-charac [] = sym drop0
x+x-charac (true âˆ· xs) = ap (false âˆ·_) (ap sucListBin (x+x-charac xs))
x+x-charac (false âˆ· xs) = ap (false âˆ·_) (x+x-charac xs)
x+x-charac (drop0 i) j =
  hcomp (Î» k â†’ Î» {(i = i0) â†’ false âˆ· drop0 (~ j âˆ¨ ~ k)
                 ; (i = i1) â†’ drop0 (~ j)
                 ; (j = i0) â†’ drop0 i
                 ; (j = i1) â†’ false âˆ· drop0 (i âˆ¨ ~ k)})
        (drop0 (~ j âˆ§ i))

suc-x+x-charac : (xs : ListBin) â†’ sucListBin (xs +LB xs) â‰¡ (true âˆ· xs)
suc-x+x-charac xs = ap sucListBin (x+x-charac xs)

ListBinâ†’â„•â†’ListBin : (x : ListBin) â†’ â„•â†’ListBin (ListBinâ†’â„• x) â‰¡ x
ListBinâ†’â„•â†’ListBin [] = refl
ListBinâ†’â„•â†’ListBin (true âˆ· xs) =
    ap sucListBin (â„•â†’ListBin-pres+ (ListBinâ†’â„• xs) (ListBinâ†’â„• xs)
                âˆ™ ap (Î» x â†’ x +LB x) (ListBinâ†’â„•â†’ListBin xs))
  âˆ™ suc-x+x-charac xs
ListBinâ†’â„•â†’ListBin (false âˆ· xs) =
    (â„•â†’ListBin-pres+ (ListBinâ†’â„• xs) (ListBinâ†’â„• xs)
  âˆ™ (ap (Î» x â†’ x +LB x) (ListBinâ†’â„•â†’ListBin xs)))
  âˆ™ x+x-charac xs
ListBinâ†’â„•â†’ListBin (drop0 i) = help i
  where
  lem : (refl âˆ™ refl) âˆ™ sym drop0 â‰¡ sym drop0
  lem = ap (_âˆ™ sym drop0) (rUnit refl) âˆ™  lUnit (sym drop0)

  help : PathP (Î» i â†’ [] â‰¡ drop0 i) ((refl âˆ™ refl) âˆ™ sym drop0) refl
  help i j = hcomp (Î» k â†’ Î» {(i = i0) â†’ lem (~ k) j
                            ; (i = i1) â†’ []
                            ; (j = i0) â†’ []
                            ; (j = i1) â†’ drop0 i})
                   (drop0 (i âˆ¨ ~ j))

â„•â†’ListBinâ†’â„• : (x : â„•) â†’ ListBinâ†’â„• (â„•â†’ListBin x) â‰¡ x
â„•â†’ListBinâ†’â„• zero = refl
â„•â†’ListBinâ†’â„• (suc x) =
    ListBinâ†’â„•-suc (â„•â†’ListBin x)
  âˆ™ ap suc (â„•â†’ListBinâ†’â„• x)

â„•â‰ƒListBin : â„• â‰ƒ ListBin
â„•â‰ƒListBin = isoToEquiv (iso  â„•â†’ListBin ListBinâ†’â„• ListBinâ†’â„•â†’ListBin â„•â†’ListBinâ†’â„•)

```
# Part 3: The SIP
### Exericise 8 (â˜…â˜…)
Show that, using an SIP inspired argument, if `(A , _+A_)` is a semigroup and `(B , _+B_)` is some other type with a composition satisfying:

(i) `e : A â‰ƒ B`

(ii) `((x y : A) â†’ e (x +A y) â‰¡ e x +B e y`

then `(B , _+B_)` defines a semigroup.

Conclude that `(ListBin , _+LB_)` is a semigroup

For inspiration, see Lecture9-notes
```agda


module _ {A B : Type} (SA : SemiGroup A) (e : A â‰ƒ B)
  (_+B_ : B â†’ B â†’ B)
  (hom : (x y : A) â†’ prâ‚ e (prâ‚ SA x y) â‰¡ ((prâ‚ e x) +B prâ‚ e y))
  where
  f = prâ‚ e
  fâ» = invEq e

  _+A_ = prâ‚ SA

  SemiGroupSIPAux : SemiGroup B
  SemiGroupSIPAux = substEquiv SemiGroup e SA

  _+B'_ = prâ‚ SemiGroupSIPAux

  +B'â‰¡+B : (x y : B) â†’ x +B' y â‰¡ (x +B y)
  +B'â‰¡+B x y =
    (x +B' y)                                          â‰¡âŸ¨ transportRefl (f (fâ» (transport refl x)
                                                                        +A (fâ» (transport refl y)))) âŸ©
    f (fâ» (transport refl x) +A fâ» (transport refl y)) â‰¡âŸ¨ (Î» i â†’ f (fâ» (transportRefl x i)
                                                         +A fâ» (transportRefl y i))) âŸ©
    f (fâ» x +A fâ» y)                                   â‰¡âŸ¨ hom (fâ» x) (fâ» y) âŸ©
    (f (fâ» x) +B f (fâ» y))                             â‰¡âŸ¨ (Î» i â†’ secEq e x i +B secEq e y i) âŸ©
    x +B y âˆ

  assoc+B : âˆ€ m n o â†’ m +B (n +B o) â‰¡ (m +B n) +B o
  assoc+B m n o =
     (m +B (n +B o))          â‰¡âŸ¨ (Î» i â†’ +B'â‰¡+B m (+B'â‰¡+B n o (~ i)) (~ i)) âŸ©
     (m +B' (n +B' o))        â‰¡âŸ¨ prâ‚‚ SemiGroupSIPAux m n o âŸ©
     ((m +B' n) +B' o)        â‰¡âŸ¨ (Î» i â†’ +B'â‰¡+B (+B'â‰¡+B m n i) o i) âŸ©
     ((m +B n) +B o) âˆ

  inducedSemiGroup : SemiGroup B
  inducedSemiGroup = _+B_ , assoc+B

SemiGroupListBin : SemiGroup ListBin
SemiGroupListBin = inducedSemiGroup SemiGroupâ„• â„•â‰ƒListBin _+LB_ â„•â†’ListBin-pres+
